plugins {
    id "org.sonarqube" version "1.1"
}

allprojects {
    apply plugin: 'java'
    group = 'com.github.clboettcher.bonappetit'
    version = '1.0.0'
}

// Config for all bonappetit (sub)projects
subprojects {
    repositories {
        mavenCentral()
    }

    dependencies {
        // Utils
        compile 'com.google.guava:guava:19.0'
        compile 'org.apache.commons:commons-lang3:3.0'
    }

    // Tasks that adds the GPL license header info to all *.java files.
    task 'add-license-headers' << {
        def licenseHeader = new File("${rootProject.projectDir}/LICENSE-header").text
        def srcDir = new File("${project.projectDir}/src")
        println "srcDir: ${srcDir.path}"
        srcDir.eachFileRecurse { file ->
            if (file.name.endsWith(".java") && !file.text.contains(licenseHeader)) {
                println "Adding license header to '${file.path}'"
                // Remove everything before 'package' and overwrite file including license header
                file.text = licenseHeader + '\n' + file.text.substring(file.text.indexOf('package'))
            }
        }
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.9'
}