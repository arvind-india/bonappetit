plugins {
    id "org.sonarqube" version "1.1"
}

// Import common dependency declarations
apply from: 'scripts/dependencies.gradle'

// Config for all subprojects
subprojects {
    repositories {
        mavenCentral()
    }

    // Tasks that adds the GPL license header info to all *.java files.
    task 'add-license-headers' << {
        def licenseHeader = new File("${rootProject.projectDir}/LICENSE-header").text
        def srcDir = new File("${project.projectDir}/src")
        println "srcDir: ${srcDir.path}"
        srcDir.eachFileRecurse { file ->
            if (file.name.endsWith(".java") && !file.text.contains(licenseHeader)) {
                println "Adding license header to '${file.path}'"
                // Remove everything before 'package' and overwrite file including license header
                file.text = licenseHeader + '\n' + file.text.substring(file.text.indexOf('package'))
            }
        }
    }
}


task wrapper(type: Wrapper) {
    gradleVersion = '2.9'
}