plugins {
    id "com.github.hierynomus.license" version "0.13.1"
}

apply from: "scripts/dependencyVersions.gradle"

// Config for all bonappetit projects
allprojects {
    group = 'com.github.clboettcher.bonappetit'
    version = '0.0.1-SNAPSHOT'
}

// Config for all bonappetit subprojects
subprojects {
    apply plugin: 'com.github.hierynomus.license'

    if (!project.name.equals("bonappetit-documentation")) {
        apply plugin: 'java'

        sourceCompatibility = 1.8
        targetCompatibility = 1.8
    }

    ext {
        springBootVersion = '1.3.6.RELEASE'
    }

    repositories {
        mavenCentral()
        jcenter()
    }

    license {
        mapping {
            java = 'SLASHSTAR_STYLE'
        }
        header file("$rootProject.projectDir/LICENSE-header")
        strictCheck true
        include '**/*.java'
        exclude '**/*Mapper*.java'
        exclude '**/package-info.java'
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.14.1'
}