plugins {
    id 'org.asciidoctor.convert' version '1.5.3'
    id 'com.github.jruby-gradle.base' version '1.2.1'
}

apply plugin: 'org.asciidoctor.convert'

dependencies {
    gems 'rubygems:asciidoctor-diagram:1.4.0'
}

asciidoctorj {
    version = '1.5.4'
}

asciidoctor {
    dependsOn jrubyPrepare
    requires = ['asciidoctor-diagram']
    gemPath = jrubyPrepare.outputDir
    attributes 'build-gradle': file('build.gradle'),
            'source-highlighter': 'coderay',
            'imagesdir': 'images',
            'toc': 'left',
            'icons': 'font',
            'setanchors': 'true',
            'idprefix': '',
            'idseparator': '-',
            'docinfo1': 'true'
}

task cleanGhPages(type: Delete) {
    group = 'documentation'
    def dirName = "gh-pages"
    file(dirName).listFiles().each {
        f ->
            if (!f.name.equals(".git")) {
                println "Deleting $f"
                delete f
            }
    }
}

task publishGhPages(type: Copy, dependsOn: ['asciidoctor', 'cleanGhPages']) {
    group = 'documentation'
    description = """Copies the html build output of the asciidoctor task to the gh-pages branch location.
                     Needs to be committed and pushed manually."""
    from file("${buildDir}/asciidoc/html5")
    into file("${projectDir}/gh-pages")
}