buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:1.2.3'
        classpath 'com.neenbedankt.gradle.plugins:android-apt:1.4'
    }
}
apply plugin: 'com.android.application'
apply plugin: 'android-apt'

repositories {
    jcenter()
}

// Dependency versions
def AAVersion = '3.3.1'
def jacksonVersion = '2.5.4'

dependencies {
    // Local
    compile fileTree(dir: 'libs', include: ['*.jar'])

    // Android Annotations
    apt "org.androidannotations:androidannotations:$AAVersion"
    // specify AA as an annotation processor that should only be used at compile time
    compile "org.androidannotations:androidannotations-api:$AAVersion"

    // DB
    compile 'com.j256.ormlite:ormlite-core:4.48'
    compile 'com.j256.ormlite:ormlite-android:4.48'

    // Time
    compile 'joda-time:joda-time:2.8.1'
    compile 'org.joda:joda-convert:1.7'

    // Rest
    compile 'org.springframework.android:spring-android-rest-template:1.0.1.RELEASE' // @Rest uses Spring RestTemplates
    compile "com.fasterxml.jackson.core:jackson-databind:$jacksonVersion"
    // Rest Converter uses Jackson for deserialization
    compile "com.fasterxml.jackson.core:jackson-core:$jacksonVersion"
    compile "com.fasterxml.jackson.datatype:jackson-datatype-joda:$jacksonVersion"

    // Lombok
    apt "org.projectlombok:lombok:1.16.4"
    // Specify Lombok as annotation processor that should only be used at compile time
    compile "org.projectlombok:lombok:1.16.4"

    // Utils
    compile 'com.google.guava:guava:18.0'
    compile 'org.apache.commons:commons-lang3:3.0'
}

apt {
    arguments {
        androidManifestFile variant.outputs[0].processResources.manifestFile
        // if you have multiple outputs (when using splits), you may want to have other index than 0

        // You can set optional annotation processing options here, like these commented options:
        // logLevel 'INFO'
        // logFile '/var/log/aa.log'
    }
}

android {
    compileSdkVersion 17
    buildToolsVersion "22.0.1"

    defaultConfig {
        applicationId "de.cb.playground.androidannotations.app"
        minSdkVersion 17
        targetSdkVersion 21
        versionCode 1
        versionName "1.0"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    // Exclude some txt files that cause problems when packaging, see
    // http://stackoverflow.com/questions/20673625/android-gradle-plugin-0-7-0-duplicate-files-during-packaging-of-apk
    packagingOptions {
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/license.txt'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/notice.txt'
    }
}